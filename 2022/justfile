set dotenv-load

check-and-test-all:
	for dir in $(fd -t d day); do just _check-and-test $dir; done

_check-and-test dir:
	#!/usr/bin/env bash
	cd {{dir}}
	cargo check
	cargo test
	cargo test --release

copy-scaffolding day="$(date +'%d' | sed 's/^0//')":
	mkdir -p "day-{{day}}"
	cp -r ./scaffolding/rust/* "day-{{day}}"
	fd -e rs -e toml . "day-{{day}}/" -X sd -s "DAYNUM" "{{day}}"

get-input day year:
	#!/usr/bin/env python

	from aocd import get_data
	with open('{{invocation_directory()}}/input.txt', 'w') as f:
		f.write(get_data(day={{day}}, year={{year}}) + '\n')

get-todays-input:
	just get-input "$(date +'%d' | sed 's/^0//')" "$(date +'%Y')"
